package com.steria.geodashboard.brms.geodashboard_ams_rules.rule;

import com.steria.geodashboard.brms.common.model.ConfigRocWithConfirmationPeriodLevelDuration;
import com.steria.geodashboard.brms.common.model.TagAlert;
import com.steria.geodashboard.brms.common.model.TagRocWithConfirmationPeriodLevelDuration;

// 1. Condition to raise alert: (lowerThld check) || (upperThld check)
// 2. value is compulsory
// 3. Empty lowerThld/upperThld implies no threshold for lower/upper bound applied
// 4. For rangeAlertStatus, 0=no alert, 1=lower threshold hit alert, 2=upper threshold hit alert

rule "Check Lower Rate Of Change With Confirmation Period Level Duration Threshold - Intermediate"
	ruleflow-group "Check_Threshold"
	dialect "mvel"
	when
		$alert : TagAlert(durationIntermediateAlert == false)
		$tag : TagRocWithConfirmationPeriodLevelDuration(this == $alert.tag, rocValue != null)
		$config : ConfigRocWithConfirmationPeriodLevelDuration(this == $alert.config, startTime != null, endTime != null
			, $tag.currentDate.before(endTime)
			, $tag.currentDate.equals(startTime) || $tag.currentDate.after(startTime)
			, (lowerThld != null && (lowerThld >= $tag.rocValue || ($tag.originalTrackingRocValue!=null && lowerThld >=$tag.originalTrackingRocValue)) && $tag.alertStartDate==null) ||
				($tag.alertStartDate != null && alertActiveDurationPeriod!=null &&
				$tag.currentDate.after(getDateAfterDuration($tag.alertStartDate, alertActiveDurationPeriod)) == false))
	then
		$alert.setDurationIntermediateAlert(true);
		printTagAlert("Check_Threshold", "Check Lower Rate Of Change With Confirmation Period Level Duration Threshold - Intermediate", $alert, logger);
		update($alert);
end

rule "Check Upper Rate Of Change With Confirmation Period Level Duration Threshold - Intermediate"
	ruleflow-group "Check_Threshold"
	dialect "mvel"
	when
		$alert : TagAlert(durationIntermediateAlert == false)
		$tag : TagRocWithConfirmationPeriodLevelDuration(this == $alert.tag, rocValue != null)
		$config : ConfigRocWithConfirmationPeriodLevelDuration(this == $alert.config, startTime != null, endTime != null
			, $tag.currentDate.before(endTime)
			, $tag.currentDate.equals(startTime) || $tag.currentDate.after(startTime)
			, (upperThld != null && (upperThld <= $tag.rocValue || ($tag.originalTrackingRocValue!=null && upperThld <= $tag.originalTrackingRocValue)) && $tag.alertStartDate==null) || 
				($tag.alertStartDate != null && alertActiveDurationPeriod!=null &&
				$tag.currentDate.after(getDateAfterDuration($tag.alertStartDate, alertActiveDurationPeriod)) == false))
	then
		$alert.setDurationIntermediateAlert(true);
		printTagAlert("Check_Threshold", "Check Upper Rate Of Change With Confirmation Period Level Duration Threshold - Intermediate", $alert, logger);
		update($alert);
end

rule "Check Lower Rate Of Change With Confirmation Period Level Duration Threshold - Final"
	ruleflow-group "Check_Threshold"
	dialect "mvel"
	when
		$alert : TagAlert(durationIntermediateAlert == true, hasFulfillFinalAlert == false)
		$tag : TagRocWithConfirmationPeriodLevelDuration(this == $alert.tag, originalTrackingRocValue != null)
		$config : ConfigRocWithConfirmationPeriodLevelDuration(this == $alert.config, startTime != null, endTime != null
			, $tag.currentDate.before(endTime)
			, $tag.currentDate.equals(startTime) || $tag.currentDate.after(startTime)
			, (lowerThld != null && >= $tag.originalTrackingRocValue)
			, levelPriority != null
			, alertDelayDurationPeriod == null || ($tag.alertStartDate != null && 
				$tag.currentDate.before(getDateAfterDuration($tag.alertStartDate, alertDelayDurationPeriod)) == false )
			, alertActiveDurationPeriod == null || ($tag.alertStartDate != null && 
				$tag.currentDate.after(getDateAfterDuration($tag.alertStartDate, alertActiveDurationPeriod)) == false ))
	then
		$alert.setHasFulfillFinalAlert(true);
		$alert.setFinalAlert(true);
		$alert.setAlertStatus(1);
		printTagAlert("Check_Threshold", "Check Lower Rate Of Change With Confirmation Period Level Duration Threshold - Final", $alert, logger);
		update($alert);
end

rule "Check Upper Rate Of Change With Confirmation Period Level Duration Threshold - Final"
	ruleflow-group "Check_Threshold"
	dialect "mvel"
	when
		$alert : TagAlert(durationIntermediateAlert == true, hasFulfillFinalAlert == false)
		$tag : TagRocWithConfirmationPeriodLevelDuration(this == $alert.tag, originalTrackingRocValue != null)
		$config : ConfigRocWithConfirmationPeriodLevelDuration(this == $alert.config, startTime != null, endTime != null
			, $tag.currentDate.before(endTime)
			, $tag.currentDate.equals(startTime) || $tag.currentDate.after(startTime)
			, (upperThld != null && <= $tag.originalTrackingRocValue)
			, levelPriority != null
			, alertDelayDurationPeriod == null || ($tag.alertStartDate != null && 
				$tag.currentDate.before(getDateAfterDuration($tag.alertStartDate, alertDelayDurationPeriod)) == false )
			, alertActiveDurationPeriod == null || ($tag.alertStartDate != null && 
				$tag.currentDate.after(getDateAfterDuration($tag.alertStartDate, alertActiveDurationPeriod)) == false ))
	then
		$alert.setHasFulfillFinalAlert(true);
		$alert.setFinalAlert(true);
		$alert.setAlertStatus(2);
		printTagAlert("Check_Threshold", "Check Upper Rate Of Change With Confirmation Period Level Duration Threshold - Final", $alert, logger);
		update($alert);
end

rule "Check All Rate Of Change With Confirmation Period Level Duration Threshold - True Alert"
	ruleflow-group "Check_Threshold"
	dialect "mvel"
	when
		$alert : TagAlert(finalAlert == true, hasFulfillFinalAlert == true)
		$config : ConfigRocWithConfirmationPeriodLevelDuration(levelPriority != null) from $alert.config
		//$config : ConfigRangeLevelDuration (this==$alert.config)
		$higherAlert : TagAlert(finalAlert == true, hasFulfillFinalAlert == true, alertStatus == $alert.alertStatus)
		//$higherConfig : ConfigRangeLevelDuration(this==$higherAlert.config, levelPriority > $config.levelPriority)
		$higherConfig : ConfigRocWithConfirmationPeriodLevelDuration(levelPriority!=null && > $config.levelPriority) from $higherAlert.config		
	then
		$alert.setFinalAlert(false);
		printTagAlert("Check_Threshold", "Check All Rate Of Change With Confirmation Period Level Duration Threshold - True Alert", $alert, logger);
		printConfig("Check_Threshold", "Check All Rate Of Change With Confirmation Period Level Duration Threshold - True Alert",$config,logger)
		update($alert);
end
